apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "fi.aalto.legroup.achso"
        minSdkVersion 15
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // FIXME: Remove when the Gradle bug is fixed.
    //
    // See the following:
    // https://code.google.com/p/android/issues/detail?id=61573
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()

    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
}

dependencies {
    // TODO: Convert these into Maven dependencies for easier management
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Google Play Services APIs. Android Studio will warn about this being an older version, but
    // don't listen to it. This one works fine; it will also work on devices that can't update the
    // services for some reason.
    compile 'com.google.android.gms:play-services:5.0+'

    // Google's OAuth library for OpenID Connect
    // See https://code.google.com/p/google-oauth-java-client/wiki/Setup
    compile('com.google.oauth-client:google-oauth-client:1.18.0-rc') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.android', module: 'android'
    }

    // Supports Android 4.0.3 and later (API level 15)
    compile 'com.embarkmobile:zxing-android-minimal:2.0.0@aar'

    // Convenience library to launch the scanning and encoding Activities.
    // It automatically picks the best scanning library from the above two, depending on the
    // Android version and what is available.
    compile 'com.embarkmobile:zxing-android-integration:2.0.0@aar'

    // Version 3.0.x of zxing core contains some code that is not compatible on Android 2.2 and earlier.
    // This mostly affects encoding, but you should test if you plan to support these versions.
    // Older versions e.g. 2.2 may also work if you need support for older Android versions.
    compile 'com.google.zxing:core:3.0.1'

    // Google's JSON parsing, could be replaced with Jackson
    compile 'com.google.api-client:google-api-client-gson:1.19.0'

    // Better and more resilient HTTP requests
    compile 'com.squareup.okhttp:okhttp:2.1.0'

    // REST-client
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.7.1'
}
