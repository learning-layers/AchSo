apply plugin: 'com.android.application'
apply from: 'build-helpers.gradle'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId 'fi.aalto.legroup.achso'
        minSdkVersion 16
        targetSdkVersion 21
        multiDexEnabled true

        (versionName, versionCode) = project.getGitVersions()
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()

    // An additional repo for the barcode scanner
    maven {
        url 'https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android backwards-compatibility
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'

    // Material design dialogs
    compile 'com.afollestad:material-dialogs:0.6.2.3'

    // Nice core libraries for a multitude of tasks
    compile 'com.google.guava:guava:18.0'

    // Google Play Services APIs
    compile 'com.google.android.gms:play-services:6.5.87'

    // OAuth2 library for OpenID Connect
    compile('com.google.oauth-client:google-oauth-client:1.18.0-rc') {
        exclude group: 'com.google.android'
        exclude group: 'junit'
        exclude group: 'xpp3'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    // Barcode scanning
    compile 'com.embarkmobile:zxing-android-minimal:2.0.0@aar'
    compile 'com.embarkmobile:zxing-android-integration:2.0.0@aar'
    compile 'com.google.zxing:core:3.0.1'

    // JSON parsing
    compile('com.google.api-client:google-api-client-gson:1.19.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }

    // MPEG-4 parsing
    compile 'com.googlecode.mp4parser:isoparser:1.0.5.4'

    // Image manipulation
    compile 'com.squareup.picasso:picasso:2.4.0'

    // Better and more resilient HTTP requests
    compile 'com.squareup.okhttp:okhttp:2.1.0'

    // REST-client
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.7.1'

    // Event bus
    compile 'com.squareup:otto:1.3.5'

    // Date and time formatting
    compile 'joda-time:joda-time:2.3'
}
